set dotenv-load
# and export just vars as env vars
set export

base_directory := justfile_directory()
artifact_directory := env_var_or_default('ARTIFACT_DIRECTORY', base_directory / "artifacts/")
target_directory := base_directory / "target" / "release"

# TODO: figure out a better way of handling this
shell_dylib := target_directory / "libanything_plugin_shell.dylib"
deno_plugin_dylib := target_directory / "libanything_plugin_deno.dylib"

default:
  @just --list --justfile {{justfile()}}

build-all: build-shell build-deno

# Build system shell plugin
build-shell: artifact-directory
  cd {{ base_directory / "plugins/anything-plugin-shell" }} && cargo test && cargo build --release
  @cp {{shell_dylib}} {{artifact_directory}}

build-deno: artifact-directory
  cd {{ base_directory / "plugins/anything-plugin-deno" }} && cargo test && cargo build --release
  @cp {{deno_plugin_dylib}} {{artifact_directory}}

# ensure the artifact directory exists
artifact-directory:
    #!/bin/sh
    set -eu

    mkdir -p {{artifact_directory}}

# Test all plugins
test-all: test-system-shell test-deno

# Test the system shell plugin
test-system-shell:
  cd {{ base_directory / "plugins/anything-plugin-shell" }} && cargo test

# Test the deno plugin
test-deno:
  cd {{ base_directory / "plugins/anything-plugin-deno" }} && cargo test
